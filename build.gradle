apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.deps = [
    'support': [
      'material': "com.google.android.material:material:1.1.0-beta01",
    ],
    'fastLaneScreenGrab': "tools.fastlane:screengrab:1.2.0",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.19.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.10.0'

    classpath 'com.android.tools.build:gradle:3.6.0-beta02'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.1.2'
  }
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 28
  targetSdkVersion = 28

  supportLibraryVersion = '27.0.1'

  versionCode = VERSION_CODE
  versionName = VERSION_NAME

  supportAnnotations = "androidx.annotation:annotation:1.0.0"
  supportAppCompat = "androidx.appcompat:appcompat:1.1.0"

  javaVersion = JavaVersion.VERSION_1_7
}

wrapper {
  gradleVersion = '5.6.2'
  distributionType = Wrapper.DistributionType.ALL
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  project.plugins.withId("com.android.library") {
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    project.android.libraryVariants.all {
      it.generateBuildConfigProvider.configure { it.enabled = false }
    }
  }
}
